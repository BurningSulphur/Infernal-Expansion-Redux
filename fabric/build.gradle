plugins {
    id 'com.github.johnrengelman.shadow'
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    runs {
        datagen {
            inherit client

            name "Data Generation"

            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":common").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=infernalexp"

            runDir "build/datagen"
        }
    }
}

repositories {
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
}

sourceSets.main.resources.srcDirs += project(":common").file("src/main/generated")

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:$fabric_loader_version")
    modApi("net.fabricmc.fabric-api:fabric-api:$fabric_api_version")

    modLocalRuntime("curse.maven:jei-238222:7253589")

    modCompileOnly "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modRuntimeOnly "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"

    modImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

    modApi("com.github.glitchfiend:TerraBlender-fabric:$terrablender_version")
    modApi("software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    injectAccessWidener = true
    inputFile.set shadowJar.archiveFile
}